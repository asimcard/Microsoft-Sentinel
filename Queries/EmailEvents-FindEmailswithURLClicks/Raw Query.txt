// When a malicious email is received, list all the users who received it and all the users who clicked a URL in the email, including which URLs were clicked

EmailEvents
| where EmailDirection == "Inbound"
| where Subject == "Malicious Subject Name" or SenderFromAddress == "malicioususer@domain.com"
| project RecipientEmailAddress, Subject, NetworkMessageId, SenderFromAddress
| join kind=inner (
    UrlClickEvents
    | project Clicker = AccountUpn, NetworkMessageId, Url
) on NetworkMessageId
| summarize
    ['Recipient Count'] = dcount(RecipientEmailAddress),
    ['Users Who Received Email'] = make_set(RecipientEmailAddress),
    ['Clicker Count'] = dcount(Clicker),
    ['Users Who Clicked URL'] = make_set(Clicker),
    ['URLs Clicked'] = make_set(Url)
    by SenderFromAddress, Subject, NetworkMessageId
